OUTPUT_FORMAT(binary)
ENTRY(bootstrap_entry)
EXTERN(ISR_table_begin ISR_table_end)
SECTIONS
{
  . = 0x7c00;

  .text :
  {  
    *(.text.bootstrap)
    *(.text.interrupts)
    *(.text.main) 
    *(.text.*)    
    *(.text)      
  }

  . = ALIGN(16); .isrtab     : { KEEP(*(.isrtab)) }
  . = ALIGN(16); .init_array : { KEEP(*(.init_array)) }

  . = ALIGN(16);
  .data :
  {  
  G_init_array_begin = .;
    LONG(ADDR(.init_array));
  G_init_array_end = .;
    LONG(ADDR(.init_array) + SIZEOF(.init_array));   
    
  G_bss_begin = .;
    LONG(ADDR(.bss))
  G_bss_end = .;
    LONG(ADDR(.bss) + SIZEOF(.bss))

  G_heap_begin = .;
    LONG(ADDR(.heap))

    *(.rodata.*)
    *(.rodata)
    *(.data.*)
    *(.data)
    *(.options)
  }

  . = ALIGN(16);
  .bss :
  {    
    *(.bss)
  }

  . = ALIGN(16);
  .heap :
  {}
  

  /DISCARD/ : 
  {    
    *(.eh_frame)
    *(.comment)
    *(.note.GNU-stack)
    *(.note.gnu.property)
    *(.group)
    *(.debug*)
    *(.debug_line_str)
    *(.debug_line)
    *(.debug_info)
    *(.debug_aranges)
    *(.debug_abbrev)
    *(.debug_rnglists)
  }
}
