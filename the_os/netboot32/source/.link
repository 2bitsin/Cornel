OUTPUT_FORMAT(binary)
ENTRY(bootstrap_entry)
SECTIONS
{
  . = 0x7c00;

  .text :
  {  
    *(.text.bootstrap) 
    *(.text.main) 
    *(.text.*)    
    *(.text)      
  }

  . = ALIGN(16); .isrtab     : { KEEP(*(.isrtab)) }
  . = ALIGN(16); .init_array : { KEEP(*(.init_array)) }

  . = ALIGN(16);
  .data :
  {  
  G_idtr = .;
    SHORT(SIZEOF(.isrtab));
    LONG(ADDR(.isrtab));

  G_init_array_begin = .;
    LONG(ADDR(.init_array));
  G_init_array_end = .;
    LONG(ADDR(.init_array) + SIZEOF(.init_array));   
    
  G_bss_begin = .;
    LONG(ADDR(.bss))
  G_bss_end = .;
    LONG(ADDR(.bss) + SIZEOF(.bss))

    *(.rodata.*)
    *(.rodata)
    *(.data.*)
    *(.data)    
  }

  . = ALIGN(16);
  .bss :
  {    
    *(.bss)
  }

  /DISCARD/ : 
  {    
    *(.eh_frame)
    *(.comment)
    *(.note.GNU-stack)
    *(.group)
    *(.debug*)
    *(.debug_line_str)
    *(.debug_line)
    *(.debug_info)
    *(.debug_aranges)
    *(.debug_abbrev)
    *(.debug_rnglists)
  }
}
